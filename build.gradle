buildscript {
    ext {
        springBootVersion = '2.6.5'
        jsoupVersion='1.15.3'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "io.spring.gradle:dependency-management-plugin:1.0.10.RELEASE"
    }

}

plugins {
    id "org.sonarqube" version "$sonarqubeVersion"
}

sonarqube {
    properties {
        property "sonar.projectKey", "ygtang-server"
        property "sonar.organization", "yeonggamt"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

subprojects {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'jacoco'

    jacoco {
        toolVersion = "$jacocoVersion"
    }

    group = 'com.depromeet.inspiration'
    version = '0.0.1'
    sourceCompatibility = '17'

    repositories {
        mavenCentral()
    }
    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }
    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        implementation "com.github.ulisesbocchio:jasypt-spring-boot-starter:$jasyptVersion"
        implementation 'com.github.maricn:logback-slack-appender:1.4.0'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'com.h2database:h2'
    }
    test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }
    jacocoTestReport {
        dependsOn test
        reports {
            xml.required = true
            csv.required = false
            html.required = false
        }
    }
}
